apply plugin: 'java-library'

dependencies {
    implementation project(":edison-core")
    implementation libraries.hibernate_validator
    implementation (libraries.validator_collection) {
        exclude group: 'org.hibernate', module: 'hibernate-validator'
    }
    implementation libraries.edison_hal
    api libraries.java_validation_api

    testImplementation project(":edison-testsupport")
    testImplementation test_libraries.rest_assured
    testImplementation test_libraries.commons_codec
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'edison-validation'
            from components.java
            artifact sourcesJar
            artifact javadocJar
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'edison-validation'
                description = 'Common Hibernate Validators and JSON error representations for Edison Microservices.'
                url = 'https://github.com/otto-de/jlineup'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developers {
                        developer {
                            id = 'sweiler'
                            name = 'Simon Weiler'
                        }
                    }
                }
                scm {
                    connection = 'scm:git@github.com:otto-de/edison-microservice.git'
                    developerConnection = 'scm:git@github.com:otto-de/edison-microservice.git'
                    url = 'https://github.com/otto-de/edison-microservice'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            if (project.debugUpload) {
                releasesRepoUrl = "$buildDir/repos/releases"
                snapshotsRepoUrl = "$buildDir/repos/snapshots"
            } else {
                credentials {
                    username = sonatypeUsername
                    password = sonatypePassword
                }
            }
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}