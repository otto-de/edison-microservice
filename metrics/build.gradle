
dependencies {
    compile libs.logging
    compile libs.metrics
    compile libs.springBoot
    compile("com.ryantenney.metrics:metrics-spring:3.1.0") {
        // Fix dependencies of metrics-spring 3.0.3
        exclude module: "metrics-annotation"
        exclude module: "metrics-core"
        exclude module: "metrics-healthchecks"
    }

    testCompile libs.testUnit
    testCompile project(":testsupport")
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'metrics'
                packaging 'jar'
                description 'Status library of the edison-microservice project.'
                url 'http://github.com/otto-de/edison-microservice'

                scm {
                    url 'scm:git@github.com:otto-de/edison-microservice.git'
                    connection 'scm:git@github.com:otto-de/edison-microservice.git'
                    developerConnection 'scm:git@github.com:otto-de/edison-microservice.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'gsteinacker'
                        name 'Guido Steinacker'
                    }
                }
            }
        }
    }
}
