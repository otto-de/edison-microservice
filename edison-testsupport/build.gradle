apply plugin: 'java-library'

dependencies {
    api libraries.togglz_console
    compileOnly libraries.togglz_spring_web
    compileOnly libraries.togglz_spring_boot_starter
    compileOnly libraries.aws_sdk_ssm
    compileOnly libraries.aws_sdk_s3
    compileOnly libraries.jackson_databind
    compileOnly libraries.jackson_annotations
    compileOnly libraries.mongodb_driver_core
    compileOnly libraries.mongodb_driver_sync
    compileOnly test_libraries.togglz_testing
    testImplementation test_libraries.testcontainers_mongo
    testImplementation test_libraries.testcontainers_junit

    api libraries.spring_boot
    api libraries.spring_boot_autoconfigure
    api test_libraries.junit
    api test_libraries.junit_params
    api test_libraries.junit_engine
    api test_libraries.hamcrest_core
    api test_libraries.hamcrest_library
    api test_libraries.mockito_core
    api test_libraries.spring_test
    api test_libraries.spring_boot_starter_test
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'edison-testsupport'
            from components.java
            artifact sourcesJar
            artifact javadocJar
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'edison-testsupport'
                description = 'Test support for Edison Microservices.'
                url = 'https://github.com/otto-de/jlineup'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'MediaMarco'
                        name = 'Marco Geweke'
                        email = 'marco.geweke@gmail.com'
                    }
                    developer {
                        id = 'gsteinacker'
                        name = 'Guido Steinacker'
                    }
                }
                scm {
                    connection = 'scm:git@github.com:otto-de/edison-microservice.git'
                    developerConnection = 'scm:git@github.com:otto-de/edison-microservice.git'
                    url = 'https://github.com/otto-de/edison-microservice'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            if (project.debugUpload) {
                releasesRepoUrl = "$buildDir/repos/releases"
                snapshotsRepoUrl = "$buildDir/repos/snapshots"
            } else {
                credentials {
                    username = sonatypeUsername
                    password = sonatypePassword
                }
            }
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}