apply plugin: 'java-library'

dependencies {
    implementation project(":edison-core")
    implementation libraries.spring_boot
    implementation libraries.spring_boot_autoconfigure
    implementation libraries.mongodb_driver_core
    implementation libraries.mongodb_driver_sync
    implementation libraries.mongodb_bson
    implementation libraries.hibernate_validator
    implementation libraries.java_validation_api
    implementation "org.slf4j:slf4j-api:1.7.36"

    testImplementation project(":edison-testsupport")
    testImplementation test_libraries.testcontainers_mongo
    testImplementation test_libraries.testcontainers_junit
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'edison-mongo'
            from components.java
            artifact sourcesJar
            artifact javadocJar
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'edison-mongo'
                description = 'Mongo library of the edison-microservice project.'
                url = 'https://github.com/otto-de/jlineup'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'MediaMarco'
                        name = 'Marco Geweke'
                        email = 'marco.geweke@gmail.com'
                    }
                    developer {
                        id = 'gsteinacker'
                        name = 'Guido Steinacker'
                    }
                }
                scm {
                    connection = 'scm:git@github.com:otto-de/edison-microservice.git'
                    developerConnection = 'scm:git@github.com:otto-de/edison-microservice.git'
                    url = 'https://github.com/otto-de/edison-microservice'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            if (project.debugUpload) {
                releasesRepoUrl = "$buildDir/repos/releases"
                snapshotsRepoUrl = "$buildDir/repos/snapshots"
            } else {
                credentials {
                    username = sonatypeUsername
                    password = sonatypePassword
                }
            }
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
