buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'java'
    alias(libs.plugins.jreleaser)
    alias(libs.plugins.spring.boot)
}

jreleaser {
    release {
        github {
            update {
                enabled = true
            }
        }
    }
    project {
        snapshot {
            fullChangelog = true
        }
    }
    gitRootSearch = true
    signing {
        active = 'ALWAYS'
        armored = true
    }
    deploy {
        maven {
            github {
                app {
                    snapshotSupported = true
                    active = 'ALWAYS'
                    url = "https://maven.pkg.github.com/otto-de/edison-microservice"
                    stagingRepository('build/staging-deploy')
                }
            }
            mavenCentral {
                app {
                    snapshotSupported = false
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')

                    // Time to wait between state transition checks, in seconds.
                    // Defaults to `10`.
                    //
                    retryDelay = 10

                    // Maximum number of attempts to verify state transition.
                    // Defaults to `60`.
                    // Maven Central release can currently take up to 40 minutes, so we increased this to 240
                    //
                    maxRetries = 240
                }
            }
        }
    }
}

// USE SEMANTIC VERSIONING AS SPECIFIED HERE: http://semver.org/spec/v2.0.0.html
//
// Major Release: X.0.0-RELEASE: Breaking Changes. Should be avoided if possible, or planned for future release.
// Minor Release: 0.X.0-RELEASE: Additional Features, updates from minor releases in Spring
// Micro Release: 0.0.X-RELEASE: Bugfixes, non-breaking changes, updates from micro releases in Spring
//
// DO NOT FORGET TO DOCUMENT CHANGES IN CHANGELOG.md
//
// Add a GitHub release for every new release: https://github.com/otto-de/edison-microservice/releases
// Publish artifacts to sonatype by executing the release.sh script.
//
//
def edison_version = "4.0.1-SNAPSHOT"  // <--- SET AND UPDATE EDISON VERSION HERE
//
//
//

repositories {
    mavenCentral()
}

apply from: "${rootDir}/gradle/maven.gradle"

group = 'de.otto.edison'
version = edison_version

subprojects {

    version = parent.version
    group = parent.group

    repositories {
        mavenCentral()
        mavenLocal()
    }

    if (project.name == 'edison-platform') {
        println("Configuring edison-platform project...")
        apply plugin: 'maven-publish'
    } else {
        apply plugin: 'eclipse'
        apply plugin: 'project-report'
        apply plugin: 'java-library'
        apply plugin: 'maven-publish'
        apply plugin: 'io.spring.dependency-management'


//    dependencyManagement {
//        imports {
//            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
//        }
//    }


        task allDeps(type: DependencyReportTask) {}

        apply from: "${rootDir}/gradle/idea.gradle"
        apply from: "${rootDir}/gradle/compile.gradle"
        apply from: "${rootDir}/gradle/test.gradle"
        apply from: "${rootDir}/gradle/jacoco.gradle"
        apply from: "${rootDir}/gradle/maven.gradle"
        apply from: "${rootDir}/gradle/signing.gradle"

        dependencies {
            implementation platform(project(":edison-platform"))
            compileOnly "org.springframework.boot:spring-boot-configuration-processor"
            testRuntimeOnly("org.junit.platform:junit-platform-launcher")
        }


        java {
            withJavadocJar()
            withSourcesJar()
        }

        if (!name.contains("example")) {
            publishing {
                publications {
                    mavenJava(MavenPublication) {
                        pom {
                            url = 'https://github.com/otto-de/edison-microservice'
                            licenses {
                                license {
                                    name = 'The Apache License, Version 2.0'
                                    url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                                }
                            }
                            developers {
                                developer {
                                    id = 'MediaMarco'
                                    name = 'Marco Geweke'
                                    email = 'marco.geweke@gmail.com'
                                }
                                developer {
                                    id = 'gsteinacker'
                                    name = 'Guido Steinacker'
                                }
                            }
                            scm {
                                connection = 'scm:git:https://github.com/otto-de/edison-microservice.git'
                                developerConnection = 'scm:git:ssh://github.com/otto-de/edison-microservice.git'
                                url = 'https://github.com/otto-de/edison-microservice'
                            }
                        }
                    }
                }

                repositories {
                    maven {
                        // Our settingsDirectory is the project root dir.
                        // We want to 'publish' to the specified dir to have the artifacts uploaded with JReleaser from that location afterwards.
                        url = layout.settingsDirectory.dir('build/staging-deploy')
                    }
                }
            }
        }
    }
}
