apply plugin: 'idea'
apply plugin: 'eclipse'

apply from: 'dependencies.gradle'

buildscript {
    ext.springBootVersion = "1.4.2.RELEASE"
    ext.springVersion = "4.3.4.RELEASE"

    repositories {
        maven { url "http://repo.spring.io/libs-snapshot" }
        mavenLocal()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:"+springBootVersion)
    }
}

repositories {
    mavenCentral()
    maven { url "http://repo.spring.io/libs-snapshot" }
}

idea {
    project {
        languageLevel = '1.8'
    }
}

eclipse {
  classpath {
    //customizing the classes output directory:
    defaultOutputDir = file('build/bin')

    //default settings for downloading sources and Javadoc:
    downloadSources = true
    downloadJavadoc = false
  }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'maven'
    apply plugin: 'project-report'
    apply plugin: 'signing'

    group = 'de.otto.edison'
    // Major Release: X.0.0: Breaking Changes
    // Minor Release: 0.X.0: Additional Features, updates from minor releases in Spring
    // Micro Release: 0.0.X: Bugfixes, non-breaking changes, updates from micro releases in Spring
    // DO NOT FORGET TO DOCUMENT CHANGES IN HISTORY.MD
    version = '0.80.0'

    repositories {
        mavenCentral()
        mavenLocal()
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }

    jar {
        manifest.attributes provider: 'gradle'
    }

    test {
        // pass -Dspring.profiles.active=local and other system properties to the test task
        systemProperties = System.properties

        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                println "Test Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
            }
        }
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from 'build/docs/javadoc'
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        classifier = 'sources'
    }

    task allDeps(type: DependencyReportTask) {}

}

