apply plugin: 'idea'
apply plugin: 'eclipse'

buildscript {
    repositories {
        maven { url "http://repo.spring.io/libs-snapshot" }
        mavenLocal()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.2.1.RELEASE")
    }
}

repositories {
    mavenCentral()
    maven { url "http://repo.spring.io/libs-snapshot" }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'maven'
    apply plugin: 'project-report'
    apply plugin: 'signing'

    group = 'de.otto.microservice'
    version = '0.0.1'

    targetCompatibility = "1.8"
    sourceCompatibility = "1.8"

    repositories {
        mavenCentral()
    }

    jar {
        manifest.attributes provider: 'gradle'
    }

    test {
        // pass -Dspring.profiles.active=local and other system properties to the test task
        systemProperties = System.properties

        description "Tell gradle to use TestNG with all groups"
        useTestNG()
        options {
            useDefaultListeners = true
        }
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from 'build/docs/javadoc'
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        classifier = 'sources'
    }

    ext.springBootVersion = '1.2.1.RELEASE'

    dependencies {
        compile("org.springframework.boot:spring-boot-starter-web:" + springBootVersion) {
            exclude module: "spring-boot-starter-tomcat"
        }
        compile("org.springframework.boot:spring-boot-starter-jetty:" + springBootVersion)
        compile("org.springframework.boot:spring-boot-starter-actuator:" + springBootVersion)

        compile("net.jcip:jcip-annotations:1.0")

        testCompile("ch.qos.logback:logback-core:1.0.13")
        testCompile("ch.qos.logback:logback-classic:1.0.13")

        testCompile("org.testng:testng:6.1.1")
        testCompile("org.hamcrest:hamcrest-core:1.3")
        testCompile("org.hamcrest:hamcrest-library:1.3")
        testCompile("org.mockito:mockito-core:1.9.5")

        testCompile("com.ning:async-http-client:1.8.3")

        testCompile module("org.uncommons:reportng:1.1.2") {
            dependency "org.testng:testng:6.1.1"
        }
    }
}

