apply plugin: 'java-library'

dependencies {
    compileOnly libraries.aws_sdk_ssm
    api libraries.spring_boot_starter_web
    implementation libraries.spring_boot_starter_actuator
    implementation libraries.spring_boot_starter_thymeleaf
    implementation libraries.jcip_annotations
    implementation libraries.logback_classic
    implementation libraries.spring_context_support
    implementation libraries.unboundid_ldapsdk
    implementation libraries.hibernate_validator

    testImplementation project(":edison-testsupport")
    testImplementation libraries.aws_sdk_ssm
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'edison-core'
            from components.java
            artifact sourcesJar
            artifact javadocJar
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'edison-core'
                description = 'Core library for all Edison libraries.'
                url = 'https://github.com/otto-de/jlineup'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'MediaMarco'
                        name = 'Marco Geweke'
                        email = 'marco.geweke@gmail.com'
                    }
                    developer {
                        id = 'gsteinacker'
                        name = 'Guido Steinacker'
                    }
                }
                scm {
                    connection = 'scm:git@github.com:otto-de/edison-microservice.git'
                    developerConnection = 'scm:git@github.com:otto-de/edison-microservice.git'
                    url = 'https://github.com/otto-de/edison-microservice'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            if (project.debugUpload) {
                releasesRepoUrl = "$buildDir/repos/releases"
                snapshotsRepoUrl = "$buildDir/repos/snapshots"
            } else {
                credentials {
                    username = sonatypeUsername
                    password = sonatypePassword
                }
            }
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
