apply plugin: 'java-library'

dependencies {
    implementation project(":edison-core")
    api libraries.togglz_console
    api libraries.togglz_spring_web

    implementation libraries.aws_sdk_s3
    implementation libraries.mongodb_driver_core
    implementation libraries.mongodb_driver_sync
    implementation project(":edison-mongo")

    implementation libraries.unboundid_ldapsdk
    implementation libraries.jcip_annotations
    implementation libraries.java_validation_api

    testImplementation project(":edison-testsupport")
    testImplementation project(":edison-mongo")
    testImplementation test_libraries.togglz_testing

    testImplementation libraries.aws_sdk_s3
    testImplementation test_libraries.testcontainers_mongo
    testImplementation test_libraries.testcontainers_junit
    testImplementation test_libraries.junit
    testImplementation test_libraries.mockito_core
    testImplementation test_libraries.hamcrest_core
    testImplementation test_libraries.hamcrest_library
    testImplementation test_libraries.spring_boot_starter_test
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'edison-togglz'
            from components.java
            artifact sourcesJar
            artifact javadocJar
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'edison-togglz'
                description = 'Feature toggles for Edison Microservices.'
                url = 'https://github.com/otto-de/jlineup'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'MediaMarco'
                        name = 'Marco Geweke'
                        email = 'marco.geweke@gmail.com'
                    }
                    developer {
                        id = 'gsteinacker'
                        name = 'Guido Steinacker'
                    }
                }
                scm {
                    connection = 'scm:git@github.com:otto-de/edison-microservice.git'
                    developerConnection = 'scm:git@github.com:otto-de/edison-microservice.git'
                    url = 'https://github.com/otto-de/edison-microservice'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            if (project.debugUpload) {
                releasesRepoUrl = "$buildDir/repos/releases"
                snapshotsRepoUrl = "$buildDir/repos/snapshots"
            } else {
                credentials {
                    username = sonatypeUsername
                    password = sonatypePassword
                }
            }
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}