
configurations {
  provided
}

dependencies {

    compile libs.springBootWeb
    compile libs.logging
    compile libs.jcip

    provided project(":mongo")

    testCompile project(":mongo")
    testCompile libs.fakeMongo
    testCompile project(":testsupport")
}

sourceSets.main.compileClasspath += [configurations.provided]

idea {
  module{
    scopes.PROVIDED.plus += [configurations.provided]
  }
}

eclipse {
  classpath {
    plusConfigurations += [configurations.provided]
  }
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'jobs'
                packaging 'jar'
                description 'Jobs library of the edison-microservice project.'
                url 'http://github.com/otto-de/edison-microservice'

                scm {
                    url 'scm:git@github.com:otto-de/edison-microservice.git'
                    connection 'scm:git@github.com:otto-de/edison-microservice.git'
                    developerConnection 'scm:git@github.com:otto-de/edison-microservice.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'gsteinacker'
                        name 'Guido Steinacker'
                    }
                }
            }
        }
    }
}
